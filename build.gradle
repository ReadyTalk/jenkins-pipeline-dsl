plugins {
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'com.readytalk.ci' version '0.5.3'
  id 'nebula.info' version '2.2.2'
  id 'com.jfrog.bintray' version '1.3.1'
  id 'idea'
}

subprojects {
  apply plugin: 'groovy'
  apply plugin: 'jacoco'
  apply plugin: 'maven-publish'
  apply plugin: 'com.readytalk.ci'
  apply plugin: 'com.readytalk.ci.version.snapshot'
  apply plugin: 'com.jfrog.artifactory'
  apply plugin: 'com.jfrog.bintray'

  repositories {
    jcenter()
    mavenCentral()
  }

  tasks.withType(JacocoReport) {
    onlyIf = {
      executionData = executionData.filter {
        it.exists()
      }
      !executionData.isEmpty()
    }
  }

  test {
    maxParallelForks = 2
  }

  configurations {
    //conflicts with localGroovy()
    compile.exclude module: 'groovy-all'
  }

  dependencies {
    compile (
      localGroovy(),
      //TODO: 1.38+ are missing from jcenter, contact upstream maintainers
      'org.jenkins-ci.plugins:job-dsl-core:1.37',
    )

    testCompile (
      'org.spockframework:spock-core:1.0-groovy-2.4',
      'cglib:cglib-nodep:2.2',
    )
  }

  artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory'
    publish {
      repository {
        repoKey = 'oss-snapshot-local'
        username = bintrayUser
        password = bintrayKey
        maven = true
      }
    }
  }

  bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
      name = rootProject.name
      userOrg = 'readytalk'
      repo = 'plugins'
      version.vcsTag = buildEnv.travisTag
    }

    //bintray plugin is imperative, and can't be lazily configured properly from
    //publications As a result, the publication names must be manually and
    //explicitly passed into its extension ahead of time
    publications = ['jar']
  }

  artifactoryPublish.onlyIf { ! buildEnv.release }
  bintrayUpload.onlyIf { buildEnv.release }
}

//Workaround for https://github.com/bintray/gradle-bintray-plugin/issues/74
afterEvaluate {
  task bintrayUpload(group: "publishing", overwrite: true) {
    dependsOn subprojects.bintrayUpload
  }
}

wrapper {
  gradleVersion = '2.8'
}
