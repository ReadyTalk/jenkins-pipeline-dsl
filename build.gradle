plugins {
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'com.readytalk.ci' version '0.5.2'
  id 'nebula.info' version '2.2.2'
  id 'idea'
}

subprojects {
  apply plugin: 'groovy'
  apply plugin: 'jacoco'
  apply plugin: 'maven-publish'
  apply plugin: 'com.readytalk.ci'
  apply plugin: 'com.readytalk.ci.version.snapshot'
  apply plugin: 'com.jfrog.artifactory'

  repositories {
    jcenter()
    mavenCentral()
  }

  tasks.withType(JacocoReport) {
    onlyIf = {
      executionData = executionData.filter {
        it.exists()
      }
      !executionData.isEmpty()
    }
  }

  test {
    maxParallelForks = 2
  }

  configurations {
    //conflicts with localGroovy()
    compile.exclude module: 'groovy-all'
  }

  dependencies {
    compile (
      localGroovy(),
      'org.jenkins-ci.plugins:job-dsl-core:1.34',
    )

    testCompile (
      'org.spockframework:spock-core:1.0-groovy-2.3',
      'cglib:cglib-nodep:2.2',
    )
  }
}

wrapper {
  gradleVersion = '2.5'
}
